plugins {
    id "io.freefair.git-version" version "5.3.3.3"
}

allprojects {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    group = "de.materna.gsb.tools"

    project.plugins.withId("java") {
        java {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(11))
            }
        }

        dependencies {
            implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }

        tasks.withType(Jar).configureEach {
            manifest.attributes(
                    "Implementation-Version": project.version,
                    "Implementation-Vendor": "Materna Information & Communications SE",
                    "Implementation-Vendor-Id": project.group,
                    "X-Gradle-Version": gradle.gradleVersion
            )
        }
    }

    plugins.withId("maven-publish") {
        publishing {
            publications.withType(MavenPublication) {
                pom {
                    description = project.description
                    inceptionYear = '2022'
                    organization {
                        name = 'Materna Information & Communications SE'
                        url = 'https://materna.de'
                    }
                    developers {
                        developer {
                            id = 'lgrefer'
                            name = 'Lars Grefer'
                            email = 'lars.grefer@materna.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://bitbucket.materna.de/scm/gsb/gsb-gradle-plugin.git'
                        developerConnection = 'scm:git:ssh://git@bitbucket.materna.de/gsb/gsb-gradle-plugin.git'
                        url = 'https://bitbucket.materna.de/projects/GSB/repos/gsb-gradle-plugin'
                    }
                }
            }
        }

    }
}